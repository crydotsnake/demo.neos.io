prototype(Neos.Demo:Content.Registration) {
  process {
    content >
    content = afx`
      <fieldset class="space-y-6 mb-8 max-w-[300px]">
        <Neos.Fusion.Form:FieldContainer field.name="firstName" label="First Name" attributes.class="flex flex-col">
          <Neos.Fusion.Form:Input attributes.class="form-input" />
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:FieldContainer field.name="lastName" label="Last Name" attributes.class="flex flex-col">
          <Neos.Fusion.Form:Input attributes.class="form-input" />
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:FieldContainer field.name="username" label="Username" attributes.class="flex flex-col">
            <Neos.Fusion.Form:Input attributes.class="form-input" />
            <small>(lowercase letters and numbers only)</small>
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:FieldContainer field.name="password" label="Password" attributes.class="flex flex-col">
          <Neos.Fusion.Form:Password attributes.class="form-input" />
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:FieldContainer field.name="company" label="" attributes.class="hidden">
          <Neos.Fusion.Form:Input />
        </Neos.Fusion.Form:FieldContainer>
      </fieldset>
    `

    schema {
      company =  ${Form.Schema.string().validator('StringLength', {minimum: 0, maximum: 0})}
    }
  }

  action {
    createBackendUser {
      options.roles.@process.changeRoles = ${['Neos.Neos:RestrictedEditor']}
    }

    addUserWorkspace {
      type = 'Neos.Demo.UserRegistration.Form.Runtime:AddUserWorkspace'
      options {
        username = ${data.username}
      }
      @position = 'after createBackendUser'
    }

    addUserAssetCollection {
      type = 'Neos.Demo.UserRegistration.Form.Runtime:AddUserAssetCollection'
      options {
        username = ${data.username}
      }
      @position = 'after addUserWorkspace'
    }
  }
}
